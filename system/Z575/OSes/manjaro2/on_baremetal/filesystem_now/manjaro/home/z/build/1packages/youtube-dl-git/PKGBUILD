# Maintainer: Bastien Dejean <nihilhill@gmail.com>

_pkgname=youtube-dl
pkgname=${_pkgname}-git
pkgver=13298
pkgrel=1
pkgdesc='A small command-line program to download videos from YouTube.com and a few more sites'
arch=('any')
url="http://rg3.github.com/${_pkgname}"
license=('custom')
depends=('python' 'python-setuptools')
makedepends=('git')
optdepends=('ffmpeg: for video post-processing')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
patch1="le.patch"
source=("git+https://github.com/rg3/${_pkgname}.git"
"$patch1"
)
sha512sums=('SKIP'
            'b348a07dc17432c658966a1e35a7339a887eed74330c032ec5195bc6939ac4f1a421f7274874495e3d8ee3156e6e2461ed7a7afd6411a06f5187aebe45b062e9')

pkgver() {
	cd "$_pkgname"
	git rev-list --count HEAD
}

prepare() {
	cd "$_pkgname"
	sed -i 's:etc/bash_completion.d:share/bash-completion/completions:' setup.py
  patch -Np1 -i "../$patch1"
}

package() {
	cd "$_pkgname"
	python devscripts/bash-completion.py
	python setup.py install --root="${pkgdir}/" --optimize=1
	mv "${pkgdir}/usr/share/bash-completion/completions/youtube-dl.bash-completion" \
	   "${pkgdir}/usr/share/bash-completion/completions/youtube-dl"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
