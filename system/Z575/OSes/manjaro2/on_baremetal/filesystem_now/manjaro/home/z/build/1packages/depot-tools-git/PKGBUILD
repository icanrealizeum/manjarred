# Maintainer: Adrian Perez <aperez@igalia.com>
# Contributor: rway <rway07@gmail.com>
# Contributor: wabi <aschrafl@jetnet.ch>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andreas Schrafl <aschrafl@gmail.com>
# Contributor: piojo <aur@zwell.net>
# Contributor: hack.augusto <hack.augusto@gmail.com>
# Contributor: Yen Chi Hsuan <yan12125@gmail.com>

pkgname=depot-tools-git
pkgver=r3561.6d3c290
pkgrel=1
pkgdesc='Build tools for working with Chromium development, include gclient'
arch=('any')
url='https://dev.chromium.org/developers/how-tos/install-depot-tools'
patch1='0100_dont_force-version.patch'
patch2='0200_ignore_empty_AWS_CREDENTIAL_FILE_env_var.patch'
patch3='0300_dont_hide_the_freaking_errors.patch'
patch4='0400_show_gn_command.patch'
source=("${pkgname}::git+https://chromium.googlesource.com/chromium/tools/depot_tools.git"
	'depot_tools.sh' 'repo_fix.sh' 'fixshebangs.py'
  "$patch1"
  "$patch2"
  "$patch3"
  "$patch4"
  )
license=('Custom')
depends=('python2' 'ninja' 'python2-colorama' 'python2-pylint')
makedepends=('git' 'scons' 'setconf')
optdepends=(
	'google-cloud-sdk: for gsutil and download_from_google_storage'
)
provides=('depot_tools' 'gclient')
conflicts=('gclient-svn' 'depot_tools-svn')
options=('!strip')
install="depot_tools.install"
md5sums=('SKIP'
         '837bc781c70acb2b2ec4ad4e3a878d99'
         'fb0c546a078c312aa64c1f2a31599557'
         '6791def07a8c7db0c8accc5e76679cd9'
         'a36fd28793565400bd324c5f484612f0'
         '09808115586dba93b9f4d3d21ca146b1'
         'c7e842902207c7540a0cc11704e6d478'
         '4825b6b3b38ee8cd14eeb046dadf84e9')
sha512sums=('SKIP'
            'dbd6e66dce2b142830c7f22df79f6956f7f2aa762e80c1121f1a12599a8d98230d67404319c86549f52da7e736c56231d857a0f6a2dd5139b94bf70f5d7526fa'
            'bde33ffcad42a4d554d5490b6562981ef4b9f3abebadbed909749ee05ba391da4b5acb31b901e785b6f019b4ed3f9c740ab92623dd6a87e67b4b599a0010374b'
            '33d772f68deddefce985d2820d3ef60fa730a1f3bc404cef3c8b1b517369501b9c3a07bc7b1b3df4d0589b45cbe4850f935699676c3e10c437bceffb37eb8214'
            '1fd8ec714c9cadbc5fb767971a83160e00ab165939c7c326c708e265d8872be02115f1b2d73e7620c9ed3b5e94b9ae6ebb4f61e10754b400c23153e0ef450f2e'
            '2000013cbd46b67b8842ed0706100976d431c371231096d74473f0d44b65e5052a7f1cd3e6128362f0df60b3f97867f6be6564943d0c08204d4e7e0052981176'
            '50904fa0b25840419fbed593eee14fbf633b029fc2d6d262d0dc5752162f6ad65ac96974a2c0b7b9e40fed0bea00b0f62dad83f1a7481bacf65d53bf62032fbe'
            '7211efe51917af5083b0f411ede097b3305017f47251ff7bbf75ca570dd435b68bc6d88c1293e16df8a20192006f0c7eb76e80a1f333ef91b981b070972600ec')


scripts_to_fix_exec=(
	apply_issue
	cit
	clang-format
	commit_queue
	depot-tools-auth
	download_from_google_storage
	fetch
	gclient
	git-runhooks
	gn
	roll-dep-svn
	roll-dep
)

pkgver () {
	cd "${pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare () {
	cd "${pkgname}"

	# This tools work with python2, but ArchLinux default is python3. Fix it.
	# pylint is in extra, ninja is an executable and it does not need any change.
	# gclient.py require a fix for work correctly with python2-colorama
	# Another way is make default python2, but I don't think is a good idea!
	# Fixing python scripts.
	msg "Patching scripts for python2 usage..."
	"${srcdir}/fixshebangs.py"

	# Fix gclient.py
	sed -i -r -e 's/from third_party import colorama/import colorama/' \
			  -e 's/from third_party.colorama import Fore/from colorama import Fore/' \
		gclient.py

	# Fixing scripts which use "exec python"
	for script in "${scripts_to_fix_exec[@]}"
	do
		sed -r -i -e 's/exec python/exec python2/' "${script}"
	done


  patch -Np1 -i "${srcdir}/${patch1}"
  patch -Np1 -i "${srcdir}/${patch2}"
  patch -Np1 -i "${srcdir}/${patch3}"
  patch -Np1 -i "${srcdir}/${patch4}"
}

package()
{
	# Creating directories
	install -d "${pkgdir}/opt"

	cp -r "${srcdir}/${pkgname}" "${pkgdir}/opt/depot_tools"

	# Export PATH
	install -Dm755 "${srcdir}/depot_tools.sh" "${pkgdir}/etc/profile.d/depot_tools.sh"

	# Install repo_fix.sh script
	install -Dm 755 "${srcdir}/repo_fix.sh" "${pkgdir}/opt/depot_tools"

	# Install License
	install -Dm644 "${pkgdir}/opt/depot_tools/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# Move manual pages to /usr/share/man
	install -dm755 "${pkgdir}/usr/share/man"
	mv "${pkgdir}/opt/depot_tools/man"/man[0-8] "${pkgdir}/usr/share/man/"

	# Ditto for HTML pages and README files, to /usr/share/doc
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
	mv "${pkgdir}/opt/depot_tools/man/html" "${pkgdir}/usr/share/doc/${pkgname}"
	mv "${pkgdir}/opt/depot_tools"/README*  "${pkgdir}/usr/share/doc/${pkgname}"

	# Remove stray files
	rm -r "${pkgdir}/opt/depot_tools/man"

	# We depend on the "ninja" package, so the wrapper script which chooses a
	# prebuilt version of it or makes a local build is not needed at all, so
	# those are removed and a wrapper script which runs the system-installed
	# /usr/bin/ninja is created instead.
	rm "${pkgdir}/opt/depot_tools"/ninja*
	cat > "${pkgdir}/opt/depot_tools/ninja" <<-EOF
	#! /bin/sh
	exec /usr/bin/ninja
	EOF
	chmod 755 "${pkgdir}/opt/depot_tools/ninja"

	# gsutil v4.19 is included in google-cloud-sdk, and I guess 4.19 is compatible with 4.13 and 4.15
	# download_from_google_storage.py expects version 4.15 and gsutil.py expects 4.13
	GSUTIL_PATH="${pkgdir}/opt/depot_tools/external_bin/gsutil"
	install -d "${GSUTIL_PATH}/gsutil_4.13"
	ln -s /opt/google-cloud-sdk/platform/gsutil "${GSUTIL_PATH}/gsutil_4.13/gsutil"
	ln -s "gsutil_4.13" "${GSUTIL_PATH}/gsutil_4.15"

	rm -rf "${pkgdir}/opt/depot_tools/.git"
}
