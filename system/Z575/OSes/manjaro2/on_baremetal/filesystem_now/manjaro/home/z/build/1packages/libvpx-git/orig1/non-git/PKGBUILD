# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.6.0
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc' 'gcc-libs')
makedepends=('yasm' 'git')
provides=('libvpx.so')
source=("libvpx-${pkgver}.tar.gz::https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz")
sha256sums=('e2fc00c9f60c76f91a1cde16a2356e33a45b76a5a5a1370df65fd57052a4994a')

build() {
  cd libvpx-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-pic \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-vp8 \
    --enable-postproc \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-experimental \
    --enable-spatial-svc

  make
}

package() {
  cd libvpx-${pkgver}

  make DIST_DIR="${pkgdir}"/usr install

  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim: ts=2 sw=2 et:
