# Maintainer:  Kamran Mackey <kamranm1200@gmail.com>
# Contributor: richteer <richteer at lastprime.net>
# Contributor: DrZaius <lou at fakeoutdoorsman.com>

pkgname=ffmpeg-git
pkgver=3.2.r81556.g496d97f
pkgrel=1
pkgdesc="Complete solution to record, convert and stream audio and video (git version)"
arch=('i686' 'x86_64')
license=('GPL3' 'Non-free')
url="http://ffmpeg.org/"
depends=('alsa-lib' 'bzip2' 'fontconfig' 'fribidi' 'gmp' 'gnutls' 'gsm' 'lame'
         'libass' 'libavc1394' 'libbluray' 'libiec61883' 'libmodplug' 'libpulse'
         'libsoxr' 'libssh' 'libtheora' 'libva' 'libvdpau' 'libwebp'
         'netcdf' 'opencore-amr'
         #'openjpeg' 
         'opus' 'schroedinger' 'sdl' 'speex'
         'v4l-utils' 'xvidcore' 'zlib'  'libvidstab.so' 'libvorbis.so' 'libvorbisenc.so'
         'libvpx.so' 'libx264.so' 'libx265.so' 'libfdk-aac')
makedepends=('hardening-wrapper' 'ladspa' 'libvdpau' 'yasm' 'git')
optdepends=('ladspa: LADSPA filters')
provides=('ffmpeg' 'libavcodec.so' 'libavdevice.so' 'libavfilter.so'
          'libavformat.so' 'libavresample.so' 'libavutil.so' 'libpostproc.so'
	  'libswresample.so' 'libswscale.so')
conflicts=('ffmpeg' 'ffmpeg-full-git')
patch1='handlebit_size0.patch'
source=("$pkgname"::'git+git://source.ffmpeg.org/ffmpeg.git'
"$patch1"
)
sha512sums=('SKIP'
            '8fe8708ef49e5297571094dd9a132a85c020678580ea8d763ce5a1bc90827064120b1c26cf3d8991b4f293df59f251b9952ff5fb8ba1dbd0b9bcfac7ff6d23fa')

pkgver() {
  cd "$srcdir/$pkgname"
  _ver="$(git describe --tags | sed 's|-[^.]*$||' | sed 's|^n||')"
  _rev="$(git rev-list --count HEAD)"
  _gitid="$(git rev-parse --short HEAD)"
  echo "${_ver}.r${_rev}.g${_gitid}"
}

prepare() {
  cd "$srcdir/$pkgname"

  patch -Np1 -i "../${patch1}"

	      #--enable-libopenjpeg \
              #^ linker errors! openjpeg 1.5.2-1
  ./configure --prefix=/usr \
	      --disable-debug \
	      --disable-static \
	      --enable-avisynth \
	      --enable-avresample \
	      --enable-fontconfig \
	      --enable-gnutls \
	      --enable-gpl \
	      --enable-ladspa \
	      --enable-libass \
    	      --enable-libbluray \
	      --enable-libfreetype \
    	      --enable-libfribidi \
	      --enable-libgsm \
	      --enable-libmodplug \
	      --enable-libmp3lame \
	      --enable-libopencore_amrnb \
	      --enable-libopencore_amrwb \
	      --enable-libopus \
	      --enable-libfdk-aac \
	      --enable-libpulse \
	      --enable-libschroedinger \
	      --enable-libsoxr \
	      --enable-libspeex \
	      --enable-libssh \
	      --enable-libtheora \
	      --enable-libv4l2 \
	      --enable-libvidstab \
	      --enable-libvorbis \
	      --enable-libvpx \
	      --enable-libwebp \
	      --enable-libx264 \
	      --enable-libx265 \
	      --enable-libxvid \
	      --enable-nonfree \
	      --enable-shared \
	      --enable-version3 \
	      --enable-x11grab \

}

build() {
  cd "$srcdir/$pkgname"


  #CFLAGS="-pipe -march=native -Wno-trigraphs -fno-schedule-insns2 -fno-delete-null-pointer-checks -mtune=native -fomit-frame-pointer -Ofast -fno-fast-math"
  CFLAGS="-pipe -march=native -Wno-trigraphs -fno-schedule-insns2 -fno-delete-null-pointer-checks -fomit-frame-pointer -O2"
  make EXTRA_CFLAGS="$CFLAGS"
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install install-man
  install -D -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim: ts=8 sw=8 et:
